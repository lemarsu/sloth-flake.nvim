{
  version,
  types,
}: {
  pkgs,
  package ? pkgs.neovim-unwrapped,
  dependencies ? [],
  dependenciesExtraArgs ? {},
  runtime ? null,
  init ? null,
  viAlias ? false,
  vimAlias ? false,
  vimdiffAlias ? false,
  nvimdiffAlias ? false,
  ...
} @ config: let
  inherit (builtins) map isString isPath;
  inherit (pkgs) callPackage bash lib;
  inherit (lib.strings) optionalString;
  inherit (lib.lists) optional;
  inherit (lib.trivial) flip;
  inherit (lib.attrsets) optionalAttrs;

  deps = callPackage ./deps.nix {inherit dependenciesExtraArgs types;};

  normalizedPlugins = deps.normalizePlugins dependencies;
  sloth-flake = deps.mkSlothFlakePlugin version normalizedPlugins;
  runtimePlugin = deps.mkRuntimePlugin runtime;
  plugins =
    normalizedPlugins
    ++ (
      deps.normalizePlugins
      ([sloth-flake] ++ (optional (runtime != null) runtimePlugin))
    );

  extractPlugin = p: {inherit (p) optional plugin;};
  extractPlugins = map extractPlugin;

  buildInit = {
    init ? null,
    postInit ? null,
    config ? null,
  }: let
    initStr = optionalString (! isNull init) ''
      (function()
      ${deps.textOrContent init}
      end)();

    '';

    slothCall =
      if isNull postInit
      then "require('sloth-flake').setup {}"
      else ''
        require('sloth-flake').setup {
          post_init = function()
            ${deps.textOrContent postInit}
          end,
        };
      '';

    configStr = optionalString (! isNull config) ''

      (function()
      ${deps.textOrContent config}
      end)()
    '';
  in ''
    -- Generated by sloth-flake
    ${initStr}
    ${slothCall}
    ${configStr}
  '';

  customRC =
    if isString init || isPath init
    then deps.textOrContent init
    else buildInit (optionalAttrs (! isNull init) init);

  neovimConfig =
    pkgs.neovimUtils.makeNeovimConfig {
      inherit customRC;
      plugins = extractPlugins plugins;
    }
    // {luaRcContent = customRC;};
  params =
    removeAttrs neovimConfig ["manifestRc" "neovimRcContent"]
    // {inherit viAlias vimAlias;};
  pkg = pkgs.wrapNeovimUnstable package params;
  mkDiffAlias = name:
    (flip optionalString) ''
      cat <<SH > $out/bin/${name}
      #!${bash}/bin/bash
      exec $out/bin/nvim -d "\''${@}"
      SH
      chmod 555 $out/bin/${name}
    '';
in
  builtins.seq (types.mkNeovimPkgOptions config) (pkg.overrideAttrs (final: super: {
    postBuild =
      super.postBuild
      + (mkDiffAlias "vimdiff" vimdiffAlias)
      + (mkDiffAlias "nvimdiff" nvimdiffAlias);
  }))
